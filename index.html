<html>
<head>

	
	<!-- Scripts -->
	<script src="http://ajax.googleapis.com/ajax/libs/jquery/1.10.2/jquery.min.js"></script>
	<script src="js/libs/underscore.js"></script>
	<script src="js/libs/three.js"></script>

	<!-- Stylesheets -->
	<link rel="stylesheet" type="text/css" href="style.css">
	
</head>
<body>

		<canvas id="heightmap">
			You're browser doesn't support WebGL....lets try a more modern browser?
		</canvas>

		<canvas id="viewport" tabindex='1'>
			You're browser doesn't support WebGL....lets try a more modern browser?
		</canvas>


		<script id="shader-vs" type="x-shader/x-vertex">
			attribute vec3 aVertexPosition;
			attribute vec4 aVertexSlope;

			uniform mat4 uMVMatrix;
			uniform mat4 uVMatrix;
			uniform mat4 uPMatrix;

			varying vec3 pos;
			varying float depth;
			varying float distance;

			varying vec3 normal;
			varying float slope;
			varying float zband;

			void main(void) {
				vec4 position = uPMatrix * uVMatrix * uMVMatrix * vec4(aVertexPosition, 1.0); 
				gl_Position = position;

				pos = aVertexPosition.xyz;
				depth = aVertexPosition.y/50.0;
				distance = sqrt(position.x*position.x + position.z*position.z + position.y*position.y);

				normal = aVertexSlope.xyz;
				slope = aVertexSlope.w;
				zband = clamp(slope / 800.0, 0.0, 1.0);

			}
		</script>

		<script id="shader-fs" type="x-shader/x-fragment">

			precision mediump float;
			uniform vec3 uColor;

			uniform sampler2D snow;
			uniform sampler2D ice;
			uniform sampler2D snowmud;
			uniform sampler2D volcano;
			uniform sampler2D fault;
			uniform sampler2D canyon;
			uniform sampler2D deepcave;
			uniform sampler2D bison;
			uniform sampler2D rocky;
			uniform sampler2D grass;
			uniform sampler2D gravel;

			uniform vec3 viewDirection;

			varying vec3 pos;
			varying float depth;
			varying float distance;

			varying vec3 normal;
			varying float slope;
			varying float zband;

			// Per-fragment fog
			vec3 applyFog( in vec3  rgb,       // original color of the pixel
						  in float distance ) // camera to point distance
			{
				float b = 0.00002;
				float fogAmount = 1.0 - exp( -distance*b );
				vec3  fogColor  = vec3(0.5,0.6,0.7);
				return mix( rgb, fogColor, fogAmount );
			}

			// Triplanar texture mapping
			vec4 triPlanar( sampler2D tex, vec3 coords, vec3 normal ) {
				vec4 color = texture2D( tex, mod(coords.xy, 1.0) ) * normal.z + texture2D( tex, mod(coords.xz, 1.0) ) * normal.y + texture2D( tex, mod(coords.yz, 1.0) ) * normal.x;
				return color;
			}

			//////////////////////////////////////////////////////////////////
			//	
			//	Using GLSL simplex noise from Ashima Arts
			//
			// Description : Array and textureless GLSL 2D/3D/4D simplex 
			//               noise functions.
			//      Author : Ian McEwan, Ashima Arts.
			//  Maintainer : ijm
			//     Lastmod : 20110822 (ijm)
			//     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
			//               Distributed under the MIT License. See LICENSE file.
			//               https://github.com/ashima/webgl-noise
			// 

				vec3 mod289(vec3 x) {
				  return x - floor(x * (1.0 / 289.0)) * 289.0;
				}

				vec4 mod289(vec4 x) {
				  return x - floor(x * (1.0 / 289.0)) * 289.0;
				}

				vec4 permute(vec4 x) {
					 return mod289(((x*34.0)+1.0)*x);
				}

				vec4 taylorInvSqrt(vec4 r)
				{
				  return 1.79284291400159 - 0.85373472095314 * r;
				}

				float snoise(vec3 v)
				  { 
				  const vec2  C = vec2(1.0/6.0, 1.0/3.0) ;
				  const vec4  D = vec4(0.0, 0.5, 1.0, 2.0);

				// First corner
				  vec3 i  = floor(v + dot(v, C.yyy) );
				  vec3 x0 =   v - i + dot(i, C.xxx) ;

				// Other corners
				  vec3 g = step(x0.yzx, x0.xyz);
				  vec3 l = 1.0 - g;
				  vec3 i1 = min( g.xyz, l.zxy );
				  vec3 i2 = max( g.xyz, l.zxy );

				  //   x0 = x0 - 0.0 + 0.0 * C.xxx;
				  //   x1 = x0 - i1  + 1.0 * C.xxx;
				  //   x2 = x0 - i2  + 2.0 * C.xxx;
				  //   x3 = x0 - 1.0 + 3.0 * C.xxx;
				  vec3 x1 = x0 - i1 + C.xxx;
				  vec3 x2 = x0 - i2 + C.yyy; // 2.0*C.x = 1/3 = C.y
				  vec3 x3 = x0 - D.yyy;      // -1.0+3.0*C.x = -0.5 = -D.y

				// Permutations
				  i = mod289(i); 
				  vec4 p = permute( permute( permute( 
							 i.z + vec4(0.0, i1.z, i2.z, 1.0 ))
						   + i.y + vec4(0.0, i1.y, i2.y, 1.0 )) 
						   + i.x + vec4(0.0, i1.x, i2.x, 1.0 ));

				// Gradients: 7x7 points over a square, mapped onto an octahedron.
				// The ring size 17*17 = 289 is close to a multiple of 49 (49*6 = 294)
				  float n_ = 0.142857142857; // 1.0/7.0
				  vec3  ns = n_ * D.wyz - D.xzx;

				  vec4 j = p - 49.0 * floor(p * ns.z * ns.z);  //  mod(p,7*7)

				  vec4 x_ = floor(j * ns.z);
				  vec4 y_ = floor(j - 7.0 * x_ );    // mod(j,N)

				  vec4 x = x_ *ns.x + ns.yyyy;
				  vec4 y = y_ *ns.x + ns.yyyy;
				  vec4 h = 1.0 - abs(x) - abs(y);

				  vec4 b0 = vec4( x.xy, y.xy );
				  vec4 b1 = vec4( x.zw, y.zw );

				  //vec4 s0 = vec4(lessThan(b0,0.0))*2.0 - 1.0;
				  //vec4 s1 = vec4(lessThan(b1,0.0))*2.0 - 1.0;
				  vec4 s0 = floor(b0)*2.0 + 1.0;
				  vec4 s1 = floor(b1)*2.0 + 1.0;
				  vec4 sh = -step(h, vec4(0.0));

				  vec4 a0 = b0.xzyw + s0.xzyw*sh.xxyy ;
				  vec4 a1 = b1.xzyw + s1.xzyw*sh.zzww ;

				  vec3 p0 = vec3(a0.xy,h.x);
				  vec3 p1 = vec3(a0.zw,h.y);
				  vec3 p2 = vec3(a1.xy,h.z);
				  vec3 p3 = vec3(a1.zw,h.w);

				//Normalise gradients
				  vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
				  p0 *= norm.x;
				  p1 *= norm.y;
				  p2 *= norm.z;
				  p3 *= norm.w;

				// Mix final noise value
				  vec4 m = max(0.6 - vec4(dot(x0,x0), dot(x1,x1), dot(x2,x2), dot(x3,x3)), 0.0);
				  m = m * m;
				  return 42.0 * dot( m*m, vec4( dot(p0,x0), dot(p1,x1), 
												dot(p2,x2), dot(p3,x3) ) );
				  }

			//	
			//	
			//////////////////////////////////////////////////////////////////



			void main(void) {

				// TODO: allow switching between triPlanar mapping and XZ mapping

				vec3 blending = abs( normalize(normal) );
				blending = normalize(max(blending, 0.00001)); // Force weights to sum to 1.0
				float b = (blending.x + blending.y + blending.z);
				blending /= vec3(b, b, b);

				// vec4 tex1 = triPlanar( deepcave, pos*0.01,  blending );
				// vec4 tex2 = triPlanar( fault, pos*0.001, blending );
				// vec4 tex3 = triPlanar( canyon, pos*0.01,  blending );




				vec4 colBase1;
				vec4 colBase2;
				vec4 colSlope1;
				vec4 colSlope2;
				vec4 colDetail;
				float texBaseScale   = 0.005;
				float texBaseScale2  = -0.0005;
				float texSlopeScale  = 0.001;
				float texSlopeScale2 = -0.0001;
				float texDetailScale = texBaseScale*8.0;


				// Texture Synthesis Bands
				//
				// 	Texture synthesis is applied in 2 fields: height and slope
				// 	The height band determines which texture band we're working
				// 	in, while the slope determines which texture we use within
				// 	that band. To avoid cutoff lines, I apply a fading effect
				// 	between texture/slope bands using linear interpolation
				//
				//
				//	Bands
				// 	4 [6000+] snow, ice
				// 	3 [6000,6000] snow
				// 	3 [3000,5000] snow-mud, volcano
				// 	2 [700,3000] canyon, deep-cave
				// 	1 [500,700] bison, rocky
				// 	0 [500-] dirt
				float bx;
				float by;
				float bz = zband;
				float bMult;
				float band0 = 500.0;
				float band1 = 700.0;
				float band2 = 3000.0;
				float band3 = 5000.0;
				float band4 = 6000.0;
				float band5 = 10000.0;
				vec2 steepSlopePos = vec2( pos.y, max(pos.x , pos.z)); // Allow sideways UV mapping on steep slopes; this is an artifact which can be exploited to make a stretched out look on the mountain slopes
				// vec2 steepSlopePos = pos.xz;
				vec3 norm = normalize(normal);
				if (pos.y > band4) {
					bx = 5.0;
					by = clamp((pos.y - band4) / (band5 - band4), 0.0, 1.0);
				} else if (pos.y > band3) {
					bx = 4.0;
					by = clamp((pos.y - band3) / (band4 - band3), 0.0, 1.0);
				} else if (pos.y > band2) {
					bx = 3.0;
					by = clamp((pos.y - band2) / (band3 - band2), 0.0, 1.0);
				} else if (pos.y > band1) {
					bx = 2.0;
					by = clamp((pos.y - band1) / (band2 - band1), 0.0, 1.0);
				} else if (pos.y > band0) {
					bx = 1.0;
					by = clamp((pos.y - band0) / (band1 - band0), 0.0, 1.0);
				} else {
					bx = 0.0;
					by = clamp((pos.y) / (band0), 0.0, 1.0);
				}
				float scaleRocks     = 2.0;
				float scaleRocks2    = 4.0;
				float scaleCave      = 0.5;
				float scaleVolcano   = 3.0;

				float lightIntensity = 1.0;
				float attenuation    = 0.0;
				float shininess      = 0.0;

				// TODO: tinting the cave texture on the mountain to closely match the rocks..
				vec4 tintRock = vec4(1.0,1.0,1.0,1.0)*0.2;
				vec4 tintCave = vec4(1.0,1.0,1.0,1.0)*1.0;
				//vec4 tintCave = vec4( 56.0/250.0, 50.0/250.0, 40.0/250.0, 1.0 ) * 1.2;//2.0


				if (bx < 1.0) {

					colBase1 = texture2D( fault, pos.xz * texBaseScale );
					colBase1 *= texture2D( fault, pos.xz * texBaseScale2 );

					colBase2 = texture2D( bison,  pos.xz * texBaseScale );
					colBase2 *= texture2D( bison,  pos.xz * texBaseScale2 );

					colSlope1 = texture2D( rocky, pos.xz * texSlopeScale * scaleRocks ) * tintRock;
					colSlope2 = texture2D( rocky,  pos.xz * texSlopeScale * scaleRocks ) * tintRock;

					colDetail = texture2D( gravel, pos.xz * texDetailScale );
					bMult =3.8;
					lightIntensity = 0.25;

				} else if (bx < 2.0) {

					colBase1 = texture2D( bison,  pos.xz * texBaseScale );
					colBase1 *= texture2D( bison, pos.xz * texBaseScale2 );

					colBase2 = texture2D( rocky, pos.xz * texBaseScale );
					colBase2 *= texture2D( rocky,  pos.xz * texBaseScale2 * scaleRocks2 );

					colSlope1 = texture2D( rocky,    pos.xz * texSlopeScale * scaleRocks ) * tintRock;
					colSlope2 = texture2D( deepcave, pos.xz * texSlopeScale * scaleCave ) * tintCave;

					colDetail = texture2D( gravel, pos.xz * texDetailScale );
					bMult = (3.8 - 1.8) * (1.0 - by) + 1.8;
					lightIntensity = 0.25;

				} else if (bx < 3.0) {

					colBase1 = texture2D( rocky,  pos.xz * texBaseScale );
					colBase1 *= texture2D( rocky, pos.xz * texBaseScale2 * scaleRocks2 );

					colBase2 = texture2D( rocky, pos.xz * texBaseScale * scaleRocks );
					colBase2 *= texture2D( rocky,  pos.xz * texBaseScale2 * scaleRocks2 );


					//////////////////////////////////////////////////
					//
					//	STEEP SLOPE :: TRI-PLANAR MAPPING IS AN OPTION
					// texSlopeScale = 0.001;
					// bz = 1.0;
					//
					// No tri-planar
					colSlope1 = texture2D( deepcave,  pos.xz * texSlopeScale * scaleCave ) * tintCave;
					colSlope2 = texture2D( deepcave, steepSlopePos * texSlopeScale * scaleCave ) * tintCave;

					// tri-planar mapping
					// colSlope1 = triPlanar( deepcave,  pos.xyz * texSlopeScale * scaleCave, blending ) * tintCave;
					// colSlope2 = triPlanar( deepcave, pos.xyz * texSlopeScale * scaleCave, blending ) * tintCave;
					//	
					//
					//////////////////////////////////////////////////

					colDetail = texture2D( gravel, pos.xz * texDetailScale );
					bMult =1.8;
					lightIntensity = (0.25 - 1.0) * (1.0 - by) + 1.0;


				} else if (bx < 4.0) {

					colBase1 = texture2D( rocky,  pos.xz * texBaseScale * scaleRocks );
					colBase1 *= texture2D( rocky, pos.xz * texBaseScale2 * scaleRocks2 );

					colBase2 = texture2D( snowmud, pos.xz * texBaseScale * 0.2 );
					colBase2 *= texture2D( snowmud,  pos.xz * texBaseScale2 );

					//////////////////////////////////////////////////
					//
					//	STEEP SLOPE :: TRI-PLANAR MAPPING IS AN OPTION
					// texSlopeScale = 0.001;
					// bz = 1.0;
					//
					// No tri-planar
					colSlope1 = texture2D( deepcave, steepSlopePos * texSlopeScale * scaleCave ) * tintCave;
					colSlope2 = texture2D( volcano,  pos.xz * texSlopeScale * scaleVolcano );

					// tri-planar mapping
					// colSlope1 = triPlanar( deepcave, pos.xyz * texSlopeScale * scaleCave, blending ) * tintCave;
					// colSlope2 = triPlanar( volcano,  pos.xyz * texSlopeScale * scaleVolcano, blending );
					//	
					//
					//////////////////////////////////////////////////


					colDetail = texture2D( gravel, pos.xz * texDetailScale );
					bMult =1.8;
					lightIntensity = 1.0;
					attenuation = 0.2;
					shininess = 5.0;

				} else if (bx < 5.0) {

					colBase1 = texture2D( snowmud, pos.xz * texBaseScale * 0.2 );
					colBase1 *= texture2D( snowmud, pos.xz * texBaseScale2 );

					colBase2 = texture2D( snow,    pos.xz * texBaseScale );
					colBase2 *= texture2D( snow,  pos.xz * texBaseScale2 );

					colSlope1 = texture2D( volcano, pos.xz * texSlopeScale * scaleVolcano );
					colSlope2 = texture2D( ice,     pos.xz * texSlopeScale );

					colDetail = texture2D( gravel, pos.xz * texDetailScale );
					bMult = (1.8 - 1.0) * (1.0 - by) + 1.0;
					lightIntensity = (0.6 - 1.0) * (by - 1.0) + 0.6;
					attenuation = 0.2;
					shininess = 5.0;

				} else if (bx == 5.0) {

					colBase1 = texture2D( snow, pos.xz * texBaseScale );
					colBase1 *= texture2D( snow, pos.xz * texBaseScale2 );

					colBase2 = texture2D( snow,    pos.xz * texBaseScale );
					colBase2 *= texture2D( snow,  pos.xz * texBaseScale2 );

					colSlope1 = texture2D( ice, pos.xz * texSlopeScale );
					colSlope2 = texture2D( ice,     pos.xz * texSlopeScale );

					colDetail = texture2D( gravel, pos.xz * texDetailScale );
					bMult = 1.0;
					lightIntensity = 0.6;
					attenuation = 0.2;
					shininess = 5.0;

				}




				// TODO: noise-based bumpmapping & lighting turbulence

				// float alpha = snoise(pos * 0.1);
				// float alpha2 = snoise(pos * 0.0001) * 0.2;

				// // Noise based bumpmapping
				// vec3 bump = vec3(alpha*0.5 + 0.5);
				// float E = 2.0;
				// vec3 modNormal = vec3( (bump.x-alpha) / E, (bump.y-alpha) / E, (bump.z-alpha) / E);
				// norm = normalize(norm + modNormal);

				// lightIntensity *= 3.0;

				// bMult *= 1.0 + alpha/10.0;
				// lightIntensity *= 1.0 + alpha/10.0;
				// bz = clamp(bz + alpha2, 0.0, 1.0);





				// Synthesize the texture w/ linear interpolation in heightband & slope
				// TODO: add detail texture (per height-band)
				vec4 texBase = colBase1 * (1.0 - by) + colBase2 * by;
				vec4 texSlope = colSlope1 * (1.0 - by) + colSlope2 * by;
				vec4 tex = texBase * (1.0 - bz) + texSlope * bz;
				tex *= bMult;
				// tex *= colDetail * 2.0;





				// Diffuse lighting
				vec3 lightCol = vec3(1.0, 1.0, 1.0);
				vec3 light_norm = normalize(vec3(-4.0, -1.0, 4.0));
				float lDotN = max(dot(norm, -light_norm), 0.0) * 1.0;


				// Specular lighting
				vec3 specular;
				// attenuation = 0.5; // allow specular lighting everywhere
				// shininess = 3.0; // allow constant shininess
				if (lDotN < 0.0 || attenuation == 0.0) { // light source on the wrong side?
					specular = vec3(0.0, 0.0, 0.0); // no specular reflection
				} else { // light source on the right side
					specular = vec3(1.0, 1.0, 1.0) * pow(max(0.0, dot(reflect(light_norm, -norm), -viewDirection)), shininess);
				}


				vec4 result = vec4(0.0, 0.0, 0.0, 1.0);
				result.rgb = tex.rgb * (0.7) + tex.rgb * lightCol * lDotN * lightIntensity + tex.rgb * specular * attenuation;
				result.rgb = applyFog(result.rgb, distance);


				////////////////////////////////////////////////////
				//
				// TEXTURE SYNTHESIS INDIVIDUAL CONTRIBUTIONS
				//
				// result = colBase1 * bMult; // Heightband
				// result = colSlope1 * bMult; // Slope
				// result = tex; // No lighting
				// result.rgb = tex.rgb * lDotN * 1.0; // Diffuse lighting
				// result.rgb = tex.rgb * specular * 1.0; // Specular lighting
				//
				//
				////////////////////////////////////////////////////


				////////////////////////////////////////////////////
				//
				// TEXTURE MAPS FOR QUICK VISUALIZATION
				//
				//
				//
				// result.rgb = vec3( pos.y / 5000.0 ); // Altitude map
				// result.rgb = norm; // Normal map
				// result.rgb = vec3( slope / 650.0 );	// Slope map
				//
				//
				// result.rgb *= uColor; // LOD map
				//
				//
				//
				////////////////////////////////////////////////////




				gl_FragColor = result;

			}
		</script>


		<script id="skybox-vs" type="x-shader/x-vertex">
			attribute vec3 aVertexCoord;
			attribute vec3 aTexCoord;

			uniform mat4 uMVMatrix;
			uniform mat4 uVMatrix;
			uniform mat4 uPMatrix;

			varying vec3 texPos;
			varying float distance;

			void main(void) {
				vec4 position = uPMatrix * uVMatrix * vec4(aVertexCoord, 1.0); 
				gl_Position = position;

				distance = sqrt(position.x*position.x + position.z*position.z);

				texPos = aTexCoord.xyz;
			}
		</script>

		<script id="skybox-fs" type="x-shader/x-fragment">

			precision mediump float;
			uniform samplerCube TexSampler;

			varying vec3 texPos;
			varying float distance;

			vec3 applyFog( in vec3  rgb,       // original color of the pixel
						  in float distance ) // camera to point distance
			{
				float b = 0.10;
				float fogAmount = 1.0 - 0.4;// exp( -distance*b );
				vec3  fogColor  = vec3(0.5,0.6,0.7);
				return mix( rgb, fogColor, fogAmount );
			}

			void main(void) {
				vec4 tex;

				// TODO: apply fog to skybox?
				tex = textureCube(TexSampler, texPos);
				// tex.rgb = applyFog(tex.rgb, distance);
				gl_FragColor = tex;
			}
		</script>

		<script id="water-vs" type="x-shader/x-vertex">
			attribute vec3 aVertexCoord;
			attribute vec2 aTexCoord;

			uniform mat4 uMVMatrix;
			uniform mat4 uMVYMatrix;
			uniform mat4 uVMatrix;
			uniform mat4 uPMatrix;
			uniform vec2 uOffset;

			varying vec2 texPos;
			varying vec2 weirdYTest;

			void main(void) {
				vec4 position = uPMatrix * uVMatrix * uMVMatrix * vec4(aVertexCoord, 1.0);
				gl_Position = position;

				weirdYTest = position.yz;
			
				texPos = aTexCoord.xy;
			}
		</script>

		<script id="water-fs" type="x-shader/x-fragment">

			precision mediump float;
			uniform sampler2D TexSampler;

			varying vec2 texPos;
			varying vec2 weirdYTest;

			void main(void) {
				vec4 tex;

				tex = texture2D(TexSampler, texPos.xy);
				//tex.r *= weirdYTest.x;
				//tex.b *= weirdYTest.y;
				gl_FragColor = tex;
			}
		</script>


		<script src="js/main.js"></script>
		<script src="js/viewport.js"></script>
		<script src="js/generator.js"></script>
</body>
</html>
